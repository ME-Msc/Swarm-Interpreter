Port : 14457

Action flyTo(position){
    /* 调用神经网络指导无人机飞往目的地 */
    := FlyAPI_Model(position);
}

Action pickUp(){
    /* 调用既定动作 */
    := pickUpAPI();
}

Action drop(){
    /* 调用既定动作 */
    := dropAPI();
}

Action chooseDest(){
    /* dst 接收调用结果，一个目的地 */
    dst := chooseDestAPI();
    /* 将 dst 返回给调用者 */
    return dst;
}

/* 描述 Agent 具备的执行动作能力 */
Agent Uav {
    flyTo, pickUp, drop, chooseDest;
}

/* 配送行为：拾取货物，飞往目的地 */
Behavior delivering() {
    @init{
        pickup();
        dest = chooseDest();
    }
    @goal{
        loc = getGPS();
        $ dest == loc
    }
    @routine{
        flyTo(dest);
    }
}

/* 投放行为：投放货物，更新全局计数器 */
Behavior dropGood() {
    @init{}
    @goal{}
    @routine{
        drop();
        get delivery_cnt from #delivery_count# ;
        put delivery_cnt+1 to #delivery_count# ;
    }
}

/* 配送任务：指定参与任务的无人机类型、数量，要配送的货物数量 */
Task delivery({uavA[A_start~A_end]}, goods_cnt){
    @init{
        put 0 to #delivery_count# ;
    }
    @goal{
        get delivery_cnt from #delivery_count# ;
        $ delivery_cnt == goods_cnt
    }
    @routine{
        /* order语句的无人机范围值可以用表达式 */
        /* 如 uavA[ A_start ~ A_end-1 ] */
        order uavA[A_start~A_end] {
            delivering();
            dropGood();
        }
    }
}

Main {
    /* 声明无人机类型、数量 */
    Agent Uav 5;
    /* 任务调用 */
    delivery({Uav[1~3]}, 20);
}