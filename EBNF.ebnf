/* Program */
program ::= port action_list agent_list behavior_list task_defination+ main_task

port ::= "Port" ":" integer

/* Action */
action_list ::= action+

action ::= "Action" variable "(" formal_parameters? ")" action_compound_statement

action_compound_statement ::= "{" action_statement ( action_statement )* "}"

action_statement ::= assignment_statement
                    | empty 

/* TODO: 
action_RPC_call_assignment_statement 
action_RPC_call_statement 
action_return_statement 
action_if_else 
action_call
*/

/* Agent */
agent_list ::= agent+

agent ::= "Agent" variable "{" ( variable ( "," variable )* )? ";" "}"

/* TODO: agent_call ::= "Agent" variable integer ";" */

/* Behavior */
behavior_list ::= behavior+

behavior ::= "Behavior" variable "(" formal_parameters? ")" "{" behavior_init_block behavior_goal_block behavior_routine_block "}"

behavior_init_block ::= "@init" behavior_compound_statement

behavior_goal_block ::= "@goal" "{" behavior_statement* "$" expression "}"

behavior_routine_block ::= "@routine" behavior_compound_statement ( "||" behavior_compound_statement )*

behavior_compound_statement ::= "{" behavior_statement* "}"

/* TODO: behavior_statement ::= put_statement
                        | get_statement
                        | behavior_publish_statement
                        | behavior_subscribe_statement
                        | behavior_unsubscribe_statement
                        | behavior_assignment_statement
                        | behavior_if_else
                        | behavior_call
                        | action_call
                        | empty */
behavior_statement ::= assignment_statement
                    | empty

/* Task */
task_defination ::= "Task" variable "(" formal_parameters? ")" ":" compound_statement

task_call_statement ::= variable "(" ( additive_expression ( "," additive_expression)* )? ")"

/* parameter */
formal_parameters ::= variable ( "," variable )*

/* TODO: Agent decalaration */
main_task ::= "Main" ":" compound_statement

compound_statement ::= statement_list

statement_list ::= statement ( ";" statement )*

statement ::= assignment_statement
            | task_call_statement
            | empty

assignment_statement ::= variable "=" additive_expression

empty ::= 

/* Expressions */
expression ::= logical_not_expression

logical_not_expression ::= "not"? logical_or_expression

logical_or_expression ::= logical_and_expression
                        | logical_and_expression "or" logical_or_expression

logical_and_expression ::= equality_expression
                        | equality_expression "and" logical_and_expression

equality_expression ::= relational_expression
                    | relational_expression "==" equality_expression
                    | relational_expression "!=" equality_expression

relational_expression ::= additive_expression
                        | additive_expression "<" relational_expression
                        | additive_expression "<=" relational_expression
                        | additive_expression ">" relational_expression
                        | additive_expression ">=" relational_expression

additive_expression ::= multiplicative_expression ( ( "+" | "-" ) multiplicative_expression )*

multiplicative_expression ::= primary_expression ( ( "*" | "/" | "%" ) primary_expression )*

primary_expression ::= integer
                        | ( "+" | "-" ) primary_expression
                        | "(" additive_expression ")"
                        | variable

variable ::= [a-zA-Z] ( [a-zA-Z0-9] | "_" )*

integer ::= "0" | ( [1-9] [0-9]* )